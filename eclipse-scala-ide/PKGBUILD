# Maintainer: Kristof Jozsa <kjozsa@fsdev.hu>

pkgname=eclipse-scala-ide
pkgver=3.0RC2
pkgrel=1
pkgdesc="Scala IDE for Eclipse"
arch=('any')
url="http://www.scala-ide.org/"
license=('custom')
depends=('eclipse>=3.8')

source=($pkgname-$pkgver.zip::"http://download.scala-ide.org/sdk/e38/scala210/dev/update-site.zip"
	"LICENSE"
)

md5sums=('b55e9356f506c98cb21ae6b735fa6097'
         '58b225f304aaf42c8b8738894a10cb96')

package() {
  install -D -m0644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE

  _dest="${pkgdir}"/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  cd "${srcdir}"/site

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/site/${_feature}
    else
      install -Dm644 ${_feature} "${_dest}"/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} "${_dest}"/${_plugin}
  done
}
